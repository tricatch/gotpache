plugins {
    id 'java-library'
}

group = 'tricatch'
version = '0.5.2'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}



jar {
    manifest {
        attributes(
            'Main-Class': 'tricatch.gotpache.ProxyPassServer',
            'Implementation-Title': 'gotpache',
            'Implementation-Version': version,
            'Implementation-Vendor': 'tricatch'
        )
    }
}

tasks.register('copyAllDependencies', Copy) {
    dependsOn(jar)
    from layout.buildDirectory.dir('libs')
    from configurations.runtimeClasspath
    into layout.buildDirectory.dir('dist/libs')
}

tasks.register('copyAllProerties', Copy) {
    dependsOn(copyAllDependencies)
    from "conf/proxypass.yml"
    from "conf/simplelogger.properties"
    into layout.buildDirectory.dir('dist/conf')
}

tasks.register('copyAllRun', Copy) {
    dependsOn(copyAllProerties)
    from "run/gotpache.bat"
    from "run/gotpache.sh"
    into layout.buildDirectory.dir('dist')
}


tasks.register('packageDistribution', Zip) {
    dependsOn(copyAllRun)
    from layout.buildDirectory.dir('dist')
    destinationDirectory = file("dist")
}

tasks.named('build') {
    dependsOn(copyAllDependencies)
    dependsOn(copyAllProerties)
    dependsOn(copyAllRun)
    dependsOn(packageDistribution)
}

dependencies {
    implementation 'org.slf4j:slf4j-api:2.0.6'
    implementation 'org.slf4j:slf4j-simple:2.0.6'
    implementation 'org.yaml:snakeyaml:2.2'
    implementation 'io.github.tricatch:gotpache-keytool:0.1.0'
    implementation 'org.bouncycastle:bcprov-jdk18on:1.79'
    implementation 'org.bouncycastle:bcpkix-jdk18on:1.79'
    implementation 'org.bouncycastle:bcutil-jdk18on:1.79'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.17.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.1'
    implementation 'io.github.azagniotov:ant-style-path-matcher:1.0.0'
    implementation 'com.github.f4b6a3:uuid-creator:6.1.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
    testImplementation 'org.junit.platform:junit-platform-launcher:1.10.1'
    testImplementation 'org.junit.platform:junit-platform-engine:1.10.1'
    testImplementation 'org.junit.platform:junit-platform-commons:1.10.1'
}

test {
    useJUnitPlatform()
}