plugins {
    id 'java'
}

group 'tricatch'
version '0.3.0'

repositories {
    mavenCentral()
}



jar {
    manifest {
        attributes(
            'Main-Class': 'tricatch.gotpache.ProxyPassServer',
            'Implementation-Title': 'gotpache',
            'Implementation-Version': archiveVersion,
            'Implementation-Vendor': 'tricatch'
        )
    }
}

task copyAllDependencies(type: Copy) {
    dependsOn(jar)
    from "${buildDir}/libs"
    from configurations.compileClasspath
    into "${buildDir}/dist/libs"
}

task copyAllProerties(type: Copy) {
    dependsOn(copyAllDependencies)
    from "conf/proxypass.yml"
    from "conf/simplelogger.properties"
    into "${buildDir}/dist/conf"
}

task copyAllRun(type: Copy) {
    dependsOn(copyAllProerties)
    from "run/gotpache.bat"
    from "run/gotpache.sh"
    into "${buildDir}/dist"
}


task packageDistribution(type: Zip) {
    dependsOn(copyAllRun)
    from "$buildDir/dist"
    destinationDirectory = file("dist")
}

build.dependsOn(copyAllDependencies)
build.dependsOn(copyAllProerties)
build.dependsOn(copyAllRun)
build.dependsOn(packageDistribution)

dependencies {
    implementation 'org.slf4j:slf4j-api:2.0.6'
    implementation 'org.slf4j:slf4j-simple:2.0.6'
    implementation 'org.yaml:snakeyaml:2.2'
    implementation 'org.bouncycastle:bcprov-jdk18on:1.78.1'
    implementation 'org.bouncycastle:bcpkix-jdk18on:1.78.1'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.17.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.1'
    implementation 'io.github.azagniotov:ant-style-path-matcher:1.0.0'
}
